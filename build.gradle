import org.apache.tools.ant.filters.*

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.41'
}

configurations {
    embed
    compile.extendsFrom(embed)
}

group 'net.okocraft.announcer'
version '1.0.1-SNAPSHOT'

ext.pluginName  = "Announcer"
ext.desc   = "Announcer. A tool to broadcast messages."
ext.author = "akaregi"
ext.url    = "https://github.com/okocraft/Announcer"

sourceCompatibility = 1.8

repositories {
    mavenCentral()

    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'

        content {
            includeGroup 'org.bukkit'
            includeGroup 'org.spigotmc'
        }
    }

    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    embed "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testCompile group: 'junit', name: 'junit', version: '4.12'

    compile 'com.google.guava:guava:28.0-jre'
    compileOnly 'org.spigotmc:spigot-api:1.14.4-R0.1-SNAPSHOT'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

processResources {
    filteringCharset = "UTF-8"
    filter ReplaceTokens, tokens:[
            "project.name"    : pluginName,
            "project.version" : project.property("version"),
            "project.author"  : author,
            "project.url"     : url,
            "project.description": desc,
    ]
}

jar {
    from configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }
}